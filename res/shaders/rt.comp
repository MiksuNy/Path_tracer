#version 460

layout (local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

layout (rgba32f, binding = 0) uniform image2D accumImage;

#define PI 3.14159265359
#define TWO_PI 6.28318530718
#define INFINITY 10000000.0
#define HIT_LIMIT 0.00001

const ivec2 texelCoord = ivec2(gl_GlobalInvocationID.xy);
const vec2 viewport = vec2(texelCoord) / vec2(imageSize(accumImage));
const vec2 viewportCenter = viewport - 0.5;

const vec2 accumTexSize = imageSize(accumImage);
const vec2 pixelPos = vec2(viewportCenter.x * accumTexSize.x / accumTexSize.y, viewportCenter.y);

uniform int currAccumPass;

uniform bool debugNormal;

const int maxBounces = debugNormal ? 1 : 3;

const vec3 skyColor = vec3(1.0);
const float skyIntensity = 0.5;

struct Ray { vec3 origin; vec3 direction; };
struct Camera {
	vec3 position;
	mat4 inverseView;
};
struct Material {
	vec4 baseColor;
	vec4 coatColor;
	vec4 emissionColor;
    float smoothness;
    float coatSmoothness;
	float emissionStrength;
	float ior;
	float refractionAmount;
	float coatChance;
	/* + 8 bytes of padding */
};
struct Sphere { vec3 position; float radius; uint materialIndex; /* + 12 bytes of padding */ };
struct Triangle { vec4 p1; vec4 p2; vec4 p3; uint materialIndex; /* + 12 bytes of padding */ };
struct HitInfo { vec3 hitPoint; vec3 hitNormal; float hitDist; float travelDist; bool hasHit; bool frontFace; Material hitMaterial; };
struct Node { vec4 boundsMin; vec4 boundsMax; int triIndex; int numTris; int childrenIndex; };

uniform Camera cam;

layout (std430, binding = 1) readonly buffer meshSSBO {
	Triangle meshTriangles[];
};
layout (std430, binding = 2) readonly buffer bvhSSBO {
	Node nodes[];
};
layout (std430, binding = 3) readonly buffer materialSSBO {
	Material sceneMaterials[];
};
layout (std430, binding = 4) readonly buffer sphereSSBO {
	Sphere sceneSpheres[];
};
layout (std430, binding = 5) readonly buffer triangleSSBO {
	Triangle sceneTriangles[];
};

uint NextRandom(inout uint state) {
	state = state * 747796405 + 2891336453;
	uint result = ((state >> ((state >> 28) + 4)) ^ state) * 277803737;
	result = (result >> 22) ^ result;
	return result;
}

float RandomValue(inout uint state) {
	return NextRandom(state) / 4294967295.0; // 2^32 - 1
}

float RandomValueND(inout uint state) {
	float theta = TWO_PI * RandomValue(state);
	float rho = sqrt(-2 * log(RandomValue(state)));
	return rho * cos(theta);
}

vec3 RandomDirection(inout uint state) {
	float x = RandomValueND(state);
	float y = RandomValueND(state);
	float z = RandomValueND(state);
	return normalize(vec3(x, y, z));
}

vec3 RandomInHemisphere(in vec3 normal, inout uint state) {
	vec3 randomInSphere = RandomDirection(state);
	return dot(randomInSphere, normal) > 0.0 ? randomInSphere : -randomInSphere;
}

float LengthSquared(in vec3 vec) {
	return (vec.x * vec.x + vec.y * vec.y + vec.z * vec.z);
}

// https://tavianator.com/2011/ray_box.html
bool HitAABB(in vec3 boundsMin, in vec3 boundsMax, in Ray ray) {
	vec3 invDir = (1.0/ray.direction);

	float tx1 = (boundsMin.x - ray.origin.x)*invDir.x;
	float tx2 = (boundsMax.x - ray.origin.x)*invDir.x;

	float tMin = min(tx1, tx2);
	float tMax = max(tx1, tx2);

	float ty1 = (boundsMin.y - ray.origin.y)*invDir.y;
	float ty2 = (boundsMax.y - ray.origin.y)*invDir.y;

	tMin = max(tMin, min(ty1, ty2));
	tMax = min(tMax, max(ty1, ty2));

	float tz1 = (boundsMin.z - ray.origin.z)*invDir.z;
	float tz2 = (boundsMax.z - ray.origin.z)*invDir.z;

	tMin = max(tMin, min(tz1, tz2));
	tMax = min(tMax, max(tz1, tz2));

	return tMax >= tMin && tMax >= 0;
};

HitInfo HitSphere(in Sphere sphere, in Ray ray) {
	HitInfo tempHitInfo;

    vec3 oc = vec3(ray.origin - sphere.position);
    float a = pow(length(ray.direction), 2);
    float halfB = dot(oc, ray.direction);
    float c = pow(length(oc), 2) - pow(sphere.radius, 2);
    
	float discriminant = halfB * halfB - a * c;

	float sqrtDisc = sqrt(discriminant);
	float t0 = (-halfB - sqrtDisc) / a;
	float t1 = (-halfB + sqrtDisc) / a;

	bool frontFace = t0 > 0.0;

	tempHitInfo.hasHit = t0 > HIT_LIMIT || t1 > -HIT_LIMIT;
	tempHitInfo.frontFace = frontFace;
	tempHitInfo.hitPoint = frontFace ? ray.origin + ray.direction * t0 : ray.origin + ray.direction * t1;
	tempHitInfo.hitDist = frontFace ? t0 : t1;
	tempHitInfo.travelDist = t1 - t0;
	tempHitInfo.hitNormal = frontFace ? normalize(tempHitInfo.hitPoint - sphere.position) : -normalize(tempHitInfo.hitPoint - sphere.position);
	tempHitInfo.hitMaterial = sceneMaterials[sphere.materialIndex];
	return tempHitInfo;
}

// https://en.wikipedia.org/wiki/M%C3%B6ller%E2%80%93Trumbore_intersection_algorithm
HitInfo HitTriangle(in Triangle tri, in Ray ray) {
	HitInfo tempHitInfo;
	tempHitInfo.hasHit = false;

	vec3 edge1 = tri.p2.xyz - tri.p1.xyz;
	vec3 edge2 = tri.p3.xyz - tri.p1.xyz;
	vec3 rayCrossE2 = cross(ray.direction, edge2);
	float det = dot(edge1, rayCrossE2);

	float invDet = 1.0 / det;
	vec3 s = ray.origin - tri.p1.xyz;
	float u = invDet * dot(s, rayCrossE2);

	vec3 sCrossE1 = cross(s, edge1);
	float v = invDet * dot(ray.direction, sCrossE1);
	
	float t = invDet * dot(edge2, sCrossE1);

	vec3 normal = normalize(cross(edge2, edge1));

	tempHitInfo.hasHit = t > HIT_LIMIT && !(det < 0.0 && det > -0.0) && !(u < 0 || u > 1) && !(v < 0 || u + v > 1);
	tempHitInfo.frontFace = !(det < 0.0);
	tempHitInfo.hitPoint = ray.origin + ray.direction * t;
	tempHitInfo.hitDist = t;
	tempHitInfo.hitNormal = det < 0.0 ? normal : -normal; // Determine tri normal by which side of the tri was hit
	tempHitInfo.hitMaterial = sceneMaterials[tri.materialIndex];
	return tempHitInfo;
}

void TraverseBVH(inout HitInfo result, in Ray ray) {
	Node nodeStack[4];
	int stackIndex = 0;
	nodeStack[stackIndex++] = nodes[0];

	while (stackIndex > 0) {
		Node node = nodeStack[--stackIndex];

		if (HitAABB(node.boundsMin.xyz, node.boundsMax.xyz, ray)) {
			if (node.childrenIndex != 0) {
			for (int i = node.triIndex; i < node.triIndex + node.numTris; ++i) {
					HitInfo triHit = HitTriangle(meshTriangles[i], ray);
					if (triHit.hasHit && triHit.hitDist < result.hitDist) result = triHit;
				}
			} else {
				nodeStack[stackIndex++] = nodes[node.childrenIndex + 1];
				nodeStack[stackIndex++] = nodes[node.childrenIndex];
			}
		}
	}
}

HitInfo CalculateRay(in Ray ray) {
	HitInfo closestHit;
	closestHit.hitDist = INFINITY;
	HitInfo tempHit;

	// New mesh triangle test with BVH
	//TraverseBVH(closestHit, ray);

	// Old mesh triangle test
	if (HitAABB(nodes[0].boundsMin.xyz, nodes[0].boundsMax.xyz, ray)) {
		for (int i = 0; i < meshTriangles.length(); ++i) {
			tempHit = HitTriangle(meshTriangles[i], ray);
			if (tempHit.hasHit && tempHit.hitDist < closestHit.hitDist) closestHit = tempHit;
		}
		
	}

	for (int i = 0; i < sceneTriangles.length(); ++i) {
		tempHit = HitTriangle(sceneTriangles[i], ray);
		if (tempHit.hasHit && tempHit.hitDist < closestHit.hitDist) closestHit = tempHit;
	}

	for (int i = 0; i < sceneSpheres.length(); ++i) {
		tempHit = HitSphere(sceneSpheres[i], ray);
		if (tempHit.hasHit && tempHit.hitDist < closestHit.hitDist) closestHit = tempHit;
	}

	return closestHit;
}

// https://blog.demofox.org/2017/01/09/raytracing-reflection-refraction-fresnel-total-internal-reflection-and-beers-law/
float FresnelReflectAmount(in float n1, in float n2, in vec3 normal, in vec3 incident, in float reflectivity)
{
	// Schlick aproximation
	float r0 = (n1-n2) / (n1+n2);
	r0 *= r0;
	float cosX = -dot(normal, incident);
	if (n1 > n2)
	{
	    float n = n1/n2;
	    float sinT2 = n*n*(1.0-cosX*cosX);
	    // Total internal reflection
	    if (sinT2 > 1.0)
	        return 1.0;
	    cosX = sqrt(1.0-sinT2);
	}
	float x = 1.0-cosX;
	float ret = r0+(1.0-r0)*x*x*x*x*x;
	ret = (reflectivity + (1.0-reflectivity) * ret);
	return ret;
}

vec3 RayTrace(in Ray ray, in int maxBounces, inout uint state) {
	vec3 rayColor = vec3(1);
	vec3 incomingLight = vec3(0);
	vec3 emittedLight = vec3(0);

	// BVH visualisation
//	for (int i = 0; i < nodes.length(); ++i) {
//		float color = 0.01;
//		if (HitAABB(nodes[i].boundsMin.xyz, nodes[i].boundsMax.xyz, ray)) incomingLight.xyz += color;
//	}

	// Raytracing
	int currBounces = 0;
	for (int i = 0; i < maxBounces; ++i) {

		HitInfo hitInfo = CalculateRay(ray);

		if (hitInfo.hasHit && hitInfo.hitDist < INFINITY) {
			if (debugNormal) return hitInfo.hitNormal;

			currBounces++;
			
			ray.origin = hitInfo.hitPoint;
			
			const float ior = hitInfo.frontFace ? (1.0 / hitInfo.hitMaterial.ior) : hitInfo.hitMaterial.ior;

			const bool iscoatBounce = hitInfo.hitMaterial.coatChance > RandomValue(state);
			const float fresnel = FresnelReflectAmount(ior, hitInfo.hitMaterial.ior, hitInfo.hitNormal, ray.direction, 1.0 - hitInfo.hitMaterial.refractionAmount);

			const bool isRefracted = fresnel < RandomValue(state);

			const vec3 randInHemiSphere = RandomInHemisphere(hitInfo.hitNormal, state);

			ray.direction = normalize (
				mix (
					mix (
						randInHemiSphere,
						reflect(ray.direction, hitInfo.hitNormal),
						iscoatBounce ? hitInfo.hitMaterial.coatSmoothness : hitInfo.hitMaterial.smoothness
					),
					mix (
						-randInHemiSphere,
						refract(ray.direction, hitInfo.hitNormal, ior),
						hitInfo.hitMaterial.smoothness
					),
					isRefracted
				)
			);

			ray.origin += ray.direction * 0.0002; // Make sure ray doesn't collide again on the same point

			emittedLight += hitInfo.hitMaterial.emissionColor.rgb * hitInfo.hitMaterial.emissionStrength;
			rayColor *= mix(mix(hitInfo.hitMaterial.baseColor.rgb, hitInfo.hitMaterial.coatColor.rgb, iscoatBounce), hitInfo.hitMaterial.baseColor.rgb, isRefracted);
			//vec3 absorb = exp(-hitInfo.hitMaterial.baseColor.rgb * hitInfo.travelDist); // Beer's law
			//rayColor *= mix(vec3(1), absorb, isRefracted);
			incomingLight += emittedLight * rayColor;
		} else {
			currBounces++;
			
			emittedLight += skyColor * skyIntensity;
			rayColor *= skyColor;
			incomingLight += emittedLight * rayColor;
			break;
		}
	}

	return incomingLight / currBounces;
}

void main() {
	const uint pixelIndex = uint(viewport.x / accumTexSize.x * 4294967295.0 + viewport.y / accumTexSize.y * 4294967295.0);
	uint rngState = uint(currAccumPass * pixelIndex);

	Ray ray;
	ray.origin = cam.position;

	const vec3 jitter = RandomDirection(rngState) * 0.00025; // For anti-aliasing
	ray.direction = normalize(vec3(cam.inverseView * vec4(-pixelPos.x + jitter.x, pixelPos.y + jitter.y, 1.0, 0.0)));

	vec3 rayTraceColor = RayTrace(ray, maxBounces, rngState);

	vec3 finalColor = mix(imageLoad(accumImage, texelCoord).rgb, rayTraceColor, 1.0 / currAccumPass);

	imageStore(accumImage, texelCoord, vec4(finalColor, 1.0));
}